<dice_expr> ::= <math_expr>

<math_expr> ::= <mult_expr>

<mult_expr> ::= <mult_expr> '*' <add_expr>
             |  <mult_expr> '/' <add_expr>
             |  <add_expr>

<add_expr> ::= <add_expr> '+' <paren_expr>
            |  <add_expr> '-' <paren_expr>
            |  <paren_expr>

<paren_expr> ::= '(' <math_expr> ')'
              |  <number>
              |  <grouped_roll>

<opt_number> ::= <number>
              |  <>

<number> ::= <digit_string>
          |  '[' <basic_roll> ']'

<grouped_roll> ::= <opt_number> '{' <grouped_roll_inner> '}' <grouped_extras>
                |  <basic_roll>

<grouped_roll_inner> ::= <grouped_roll_inner> ',' <math_expr>
                      |  <math_expr>

<grouped_extras> ::= <keep_expr> <success_expr>

<basic_roll> ::= <opt_number> 'd' <number> <basic_extras>
              |  <opt_number> 'd' 'F' <basic_extras>

<basic_extras> ::= <reroll_expr> <explode_expr> <keep_expr> <success_expr>

<keep_expr> ::= 'k' <number>
             |  'kl' <number>
             |  'kh' <number>
             |  'd' <number>
             |  'dl' <number>
             |  'dh' <number>
             |  <>

<reroll_expr> ::= 'r' <compare_expr>
               |  'ro' <compare_expr>
               |  <>

<explode_expr> ::= '!' <opt_compare_expr>
                |  '!!' <opt_compare_expr>
                |  'p' <opt_compare_expr>

<success_expr> ::= <explicit_compare_expr> <fail_expr>
                |  <>

<fail_expr> ::= 'f' <compare_expr>
             |  <>

<opt_compare_expr> ::= <compare_expr>
                    |  <>

<compare_expr> ::= <number>
                |  <explicit_compare_expr>

<explicit_compare_expr> ::= '=' <number>
                         |  '>' <number>
                         |  '<' <number>

<digit_string> ::= <digit_string> <digit>
                |  <digit>

<digit> ::= '0' | '1' | '2' | '3' | '4' | '5' | '6' | '7' | '8' | '9'
